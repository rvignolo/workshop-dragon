*------------+--------------+-------+------+-----++----+++---++++--+++++
* Workshop
* Learning DRAGON V5 as a production code for XSs
* Argentinean Group of Calculation and Reactor Analysis
* III Annual Meeting
*
* File desc.: Flux calculation
* Ramiro Vignolo    <ramirovignolo@gmail.com>
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* DECLARATIONS
*
LINKED_LIST
            MacLib
            Geom_G1
            Geom_G2
            Geom_G3
            Track
            Pij
            Flux ;
SEQ_BINARY  Lines ;
SEQ_ASCII   GeoMix_G1    :: FILE './GeoMix_G1.ps'   ;
SEQ_ASCII   GeoFlux_G1   :: FILE './GeoFlux_G1.ps'  ;
SEQ_ASCII   GeoMix_G2    :: FILE './GeoMix_G2.ps'   ;
SEQ_ASCII   GeoFlux_G2   :: FILE './GeoFlux_G2.ps'  ;
SEQ_ASCII   GeoMix_G3    :: FILE './GeoMix_G3.ps'   ;
SEQ_ASCII   GeoFlux_G3   :: FILE './GeoFlux_G3.ps'  ;
SEQ_ASCII   Surfil       :: FILE './Surfil.sur'     ;
SEQ_ASCII   Tracks       :: FILE './Tracks.m'       ;
*
* Modules
MODULE
       MAC: GEO: PSP: G2S: SALT: NXT: TLM: ASM: FLU: DELETE: END: ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* MACROSCOPIC XS DEFINITION
MacLib := MAC: ::
  EDIT 2     (* print to output *)
  NGRO 2     (* number of groups *)
  NMIX 4     (* number of mixtures *)
  NIFI 1     (* max number of fissile spectrum for each mix *)
  READ INPUT (* read xs from imput *)
*
  MIX  1     (* data for mixture 1: fuel *)
    TOTAL      3.62022E-1 5.72155E-1
    NUSIGF     1.86278E-2 3.44137E-1
    CHI        1.00000E-0 0.00000E-0
    NFTOT      7.22964E-3 1.41126E-1
    (* for SCAT, read the manual :) *)
    SCAT   1 1 3.33748E-1
           2 2 3.80898E-1 6.64881E-4
*
  MIX  2     (* data for mixture 2: clad *)
    TOTAL      2.74144E-1 2.80890E-1
    (* for SCAT, read the manual :) *)
    SCAT   1 1 2.72377E-1
           2 2 2.77230E-1 1.90838E-4
*
  MIX  3     (* data for mixture 3: water *)
    TOTAL      6.40711E-1 1.69131
    (* for SCAT, read the manual :) *)
    SCAT   1 1 6.07382E-1
           2 2 1.68428E-0 3.31316E-2
*
  MIX  4     (* data for mixture 4: fuel+gadolinium *)
    TOTAL      3.71785E-1 1.75000E-0
    NUSIGF     1.79336E-2 1.57929E-1
    CHI        1.00000E-0 0.00000E-0
    NFTOT      6.97904E-3 6.47524E-2
    (* for SCAT, read the manual :) *)
    SCAT   1 1 3.38096E-1
           2 2 3.83204E-1 6.92807E-4
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* GEOMETRY DEFINITION AND REPRESENTATION
*
*----
* Geometries
*----
Geom_G1 := GEO: :: CAR2D 4 4
  X- DIAG X+ REFL
  Y- REFL Y+ DIAG
  CELL FUEL FUEL FUEL FUEL
            FUEL POIS FUEL
                 FUEL FUEL
                      FUEL
  ::: FUEL := GEO: CARCEL 5
    MESHX  -0.8 0.8
    MESHY  -0.8 0.8
    RADIUS  0.0 0.35336 0.4473 0.4874 0.5 0.6
    SECT 4 0
    MIX 1 1 1 1 1 1 1 1
        1 1 1 1 1 1 1 1
        1 1 1 1 1 1 1 1
        1 1 1 1 1 1 1 1
        2 2 2 2 2 2 2 2
        3 3 3 3 3 3 3 3 3 3 3 3 ;
  ::: POIS := GEO: CARCEL 5
    MESHX  -0.8 0.8
    MESHY  -0.8 0.8
    RADIUS  0.0 0.35336 0.4473 0.4874 0.5 0.6
    SECT 4 0
    MIX 4 4 4 4 4 4 4 4
        4 4 4 4 4 4 4 4
        4 4 4 4 4 4 4 4
        4 4 4 4 4 4 4 4
        2 2 2 2 2 2 2 2
        3 3 3 3 3 3 3 3 3 3 3 3 ;
  ;
*
Geom_G2 := GEO: :: CAR2D 4 4
  X- DIAG X+ REFL
  Y- REFL Y+ DIAG
  CELL FUEL FUEL FUEL FUEL
            FUEL POIS FUEL
                 FUEL FUEL
                      FUEL
  ::: FUEL := GEO: CARCEL 2
    MESHX  -0.8 0.8 SPLITX 5
    MESHY  -0.8 0.8 SPLITY 5
    RADIUS  0.0 0.5 0.6 SPLITR 4 2
    MIX 1 2 3 ;
  ::: POIS := GEO: FUEL
    MIX 4 2 3 ;
  ;
*
Geom_G3 := GEO: :: CAR2D 4 4
  X- DIAG X+ REFL
  Y- REFL Y+ DIAG
  CELL FUEL FUEL FUEL FUEL
            FUEL POIS FUEL
                 FUEL FUEL
                      FUEL
  ::: FUEL := GEO: CAR2D 1 1
    MESHX  -0.8 0.8 SPLITX 5
    MESHY  -0.8 0.8 SPLITY 5
    MIX 3
    CLUSTER PIN
    ::: PIN := GEO: TUBE 5 2 2
      RADIUS  0.0 0.35336 0.4473 0.4874 0.5 0.6
      MIX 1 1 1 1 2
          1 1 1 1 2
          1 1 1 1 2
          1 1 1 1 2
      NPIN  1 RPIN 0.0 APIN 0.785398163397 ;
    ;
  ::: POIS := GEO: CAR2D 1 1
    MESHX  -0.8 0.8 SPLITX 5
    MESHY  -0.8 0.8 SPLITY 5
    MIX 3
    CLUSTER PIN
    ::: PIN := GEO: TUBE 5 2 2
      RADIUS  0.0 0.35336 0.4473 0.4874 0.5 0.6
      MIX 4 4 4 4 2
          4 4 4 4 2
          4 4 4 4 2
          4 4 4 4 2
      NPIN  1 RPIN 0.0 APIN 0.785398163397 ;
    ;
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* TRACKING (uncomment in order to plot the ray tracing tracks)
*
Surfil GeoMix_G1 := G2S: Geom_G1 ::
  DRAWMIX
!   DRAWNOD
  ;
*
Track Lines := SALT: Surfil ::
  EDIT 2
  TSPC 8 10.0
  LONG  (* uncomment in order to plot the ray tracing tracks *)
  ;
*
*----
* Tracking representation
*----
(* uncomment in order to plot the ray tracing tracks *)
! Tracks := TLM: Track Lines ::
!   EDIT 1
!   NTPO 1
!   DIRECTIONS NoPause DIR 0
!   ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* FLUX CALCULATION
*
Pij := ASM: MacLib Track Lines ::
  EDIT 3
  PIJ        (*default*)
  PNOR HELI  (*default*)
  ;
*
Flux := FLU: Pij MacLib Track ::
  EDIT 3
  TYPE K
  ;
*
(* PSP: doesnt work with SALT: tracking *)
! GeoFlux_G1 := PSP: Track Flux ::
!   TYPE FLUX
!   TYPE MGFLUX
!   ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* DELETE
*
Flux Pij Track Lines := DELETE: Flux Pij Track Lines ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* TRACKING
*
Lines Track := NXT: Geom_G2 ::
  EDIT 2
  TISO 24 40.0
  LONG
  ;
*
GeoMix_G2 := PSP: Track ::
  TYPE MIXTURE
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* FLUX CALCULATION
*
Pij := ASM: MacLib Track Lines ::
  EDIT 3
  PIJ        (*default*)
  PNOR HELI  (*default*)
  ;
*
Flux := FLU: Pij MacLib Track ::
  EDIT 3
  TYPE K
  ;
*
GeoFlux_G2 := PSP: Track Flux ::
  TYPE FLUX
  TYPE MGFLUX
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* DELETE
*
Flux Pij Track Lines := DELETE: Flux Pij Track Lines ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* TRACKING
*
Lines Track := NXT: Geom_G3 ::
  EDIT 2
  TISO 24 40.0
  LONG
  ;
*
GeoMix_G3 := PSP: Track ::
  TYPE MIXTURE
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
* FLUX CALCULATION
*
Pij := ASM: MacLib Track Lines ::
  EDIT 3
  PIJ        (*default*)
  PNOR HELI  (*default*)
  ;
*
Flux := FLU: Pij MacLib Track ::
  EDIT 3
  TYPE K
  ;
*
GeoFlux_G3 := PSP: Track Flux ::
  TYPE FLUX
  TYPE MGFLUX
  ;
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
*------------+--------------+-------+------+-----++----+++---++++--+++++
*
END: ;
QUIT .